buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'me.champeau.gradle:jbake-gradle-plugin:0.2'
        classpath 'org.jbake:jbake-core:2.3.2'
        classpath 'org.pegdown:pegdown:1.4.2'
//        classpath 'org.asciidoctor:asciidoctor-java-integration:0.1.4'
        classpath 'org.asciidoctor:asciidoctorj:1.5.2'
    }
}

apply plugin: 'me.champeau.jbake'
apply plugin: 'idea'
apply plugin: 'java'

jbake {
    dependsOn 'cleanBuildDir','webjars resources extraction'
    input  = file 'jbake'
    clearCache = true
    configuration['template.documentation.file'] = 'documentation.gsp'
}

configurations {
    webjars
    webjars.extendsFrom(compile)
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    webjars  'org.webjars:jquery:2.1.3'
    webjars  'org.webjars:bootstrap:3.3.2-1'
    webjars  'org.webjars:highlightjs:8.4-4'
    webjars  'org.webjars:font-awesome:4.3.0-1'
}

task('webjars resources extraction', type: Copy) {
    configurations.webjars.each {jar ->

        def config = configurations.webjars.resolvedConfiguration
        def artifact = config.resolvedArtifacts.find {
            it.file.toString() == jar.absolutePath
        }

        def upStreamVersion = "${artifact.moduleVersion.id.version}"
        upStreamVersion = upStreamVersion.replaceAll(/(-[\d.-]+)/, '')

        copy {
            from zipTree(jar)
            into file("${buildDir}/webjars-content/${artifact.name}")
        }
        copy {
            from "${buildDir}/webjars-content/${artifact.name}/META-INF/resources/webjars/${artifact.name}/${upStreamVersion}",
                 "${buildDir}/webjars-content/${artifact.name}/META-INF/resources/webjars/${artifact.name}/${artifact.moduleVersion.id.version}"

            into "${jbake.input}/assets/vendor/${artifact.name}"
        }
    }
}

task cleanBuildDir(type: Delete) {
    delete "build"
}


